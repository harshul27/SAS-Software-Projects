/* Dataset 19.12 (without Age) */
data dataset19_12;
    input SuccessRating EyeContact Gender PersonnelOfficer;
    datalines;
11.0 1 1 1
7.0 1 1 2
12.0 1 1 3
6.0 1 1 4
10.0 1 1 5
15.0 1 2 1
12.0 1 2 2
14.0 1 2 3
11.0 1 2 4
16.0 1 2 5
12.0 2 1 1
16.0 2 1 2
10.0 2 1 3
13.0 2 1 4
14.0 2 1 5
14.0 2 2 1
17.0 2 2 2
13.0 2 2 3
20.0 2 2 4
18.0 2 2 5
;
run;

/* Dataset 22.17 (with Age) */
data dataset22_17;
    input SuccessRating EyeContact Gender PersonnelOfficer Age;
    datalines;
11.0 1 1 1 42.0
7.0 1 1 2 30.0
12.0 1 1 3 47.0
6.0 1 1 4 31.0
10.0 1 1 5 35.0
15.0 1 2 1 51.0
12.0 1 2 2 35.0
14.0 1 2 3 48.0
11.0 1 2 4 38.0
16.0 1 2 5 49.0
12.0 2 1 1 43.0
16.0 2 1 2 53.0
10.0 2 1 3 40.0
13.0 2 1 4 50.0
14.0 2 1 5 49.0
14.0 2 2 1 42.0
17.0 2 2 2 47.0
13.0 2 2 3 46.0
20.0 2 2 4 59.0
18.0 2 2 5 56.0
;
run;

/*Part 1: Create Group Variable and Plot LOESS Curves*/

data analysis_data;
    set dataset22_17;
    /* Create 4-category group variable */
    Group = cats(EyeContact, Gender);
run;

proc sgplot data=analysis_data;
    title "Part 1: Success Rating vs Age with LOESS Curves";
    scatter x=Age y=SuccessRating / group=Group;
    loess x=Age y=SuccessRating / group=Group nomarkers;
run;

/* Part 2: Fit Full ANCOVA Model with Interaction & Type III Test    */

proc glm data=analysis_data;
    class EyeContact Gender;
    model SuccessRating = Age EyeContact Gender EyeContact*Gender / ss3;
    title "Part 2: Full ANCOVA Model with Interaction";
    lsmeans EyeContact*Gender / pdiff;
run;

/* Part 3: Fit Additive Model & Type III Tests */

proc glm data=analysis_data;
    class EyeContact Gender;
    model SuccessRating = Age EyeContact Gender / ss3 solution;
    title "Part 3: Additive ANCOVA Model";
    lsmeans EyeContact Gender / pdiff;
    estimate 'Eye Contact Effect' EyeContact -1 1;
    estimate 'Gender Effect' Gender -1 1;
run;


/* Part 4: Age Effect Quantification      */


proc reg data=analysis_data;
    model SuccessRating = Age;
    title "Part 4: Age Effect Quantification";
run;

/* Part 5: Diagnostic Plots               */

proc glm data=analysis_data plots=diagnostics;
    class EyeContact Gender;
    model SuccessRating = Age EyeContact Gender;
    title "Part 5: Diagnostic Plots";
    output out=diag r=residual;
run;

/* Part 6: Residual Plots                 */


proc sgplot data=diag;
    title "Part 6: Residuals vs Eye Contact";
    vbox residual / category=EyeContact;
run;

proc sgplot data=diag;
    title "Part 6: Residuals vs Gender";
    vbox residual / category=Gender;
run;

proc sgplot data=diag;
    title "Part 6: Residuals vs Age";
    scatter x=Age y=residual;
    loess x=Age y=residual / nomarkers;
run;